#
# HISTORY
#  11-Sep-85  Avadis Tevanian (avie) at Carnegie-Mellon University
#	Added extra functionality for detecting new or deleted files.
#	Does not detect new directories, so when new directories are
#	created, dummy directories should be created in /usr/sys before
#	running diffsys.
#
rm -f diffs
if ($#argv == 0) then
	set target = /usr/sys
else
	set target = $1
endif

foreach dir (	accent bbnnet bin ca caif caio conf emul h kern lib  mp \
		net netimp netinet netns oaccent sensor src stand stand.4.2 standca \
		sync sys vax vaxif vaxmba vaxuba vm)
	diff -r $dir $target/$dir >>& diffs
end
sed '/^Common/d\
/^Binary/d' diffs >Diffs
grep "^diff -r" Diffs | sed "s?^.*$target/*??" > ChangedFiles
grep "^Only" Diffs > t1
grep "$target" t1 > t2
sed -e "s?^.*$target/*??" -e 's?: ?/?' t2 > DeletedFiles
grep -v "$target" t1 > t2
sed -e 's?Only in ??' -e 's?: ?/?' t2 > NewFiles
rm diffs t1 t2
